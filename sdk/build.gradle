apply plugin: 'com.android.library'
apply from: 'fat-aar.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

//    publishNonDefault true
    flavorDimensions rootProject.ext.SDKProperties[0].product
    productFlavors {
        rootProject.ext.SDKProperties.each {
            productFlavors.create(it.product)
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    if (rootProject.ext.debug) {
        implementation project(':base')
    } else {
        embedded project(':base')
        embedded project(':http')
    }
    alibabaImplementation files('libs/gaode-map.jar')
//    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportVersion}"
    testImplementation 'junit:junit:4.12'
}

rootProject.ext.SDKProperties.each {
    def product = it.product
    if (it.packageName == "" || it.packageName.length() <= 0)
        return
    // 如果渠道对应的sdk源码包不存在，则从base复制一份，根据base进行修改
    if (!file("src/$product").exists()) {
        copy {
            from "src/baidu"
            into "src/$product"
        }
    }
    // 如果渠道对应的demo源码包不存在，则从base复制一份，根据base进行修改
    if (!file("../app/src/$product").exists()) {
        copy {
            from "../app/src/baidu"
            into "../app/src/$product"
        }
    }
    ant.replaceregexp(match:'_[a-z]+_BUILD_TIME:(20\\d{6}_\\d{6}.\\d{3})?',
            replace:"_${product}_BUILD_TIME:${rootProject.ext.buildTime}", byline:true) {
        fileset(dir: "src/$product/java/com/tobenull/sdk", includes: 'SDK.java')
    }
}