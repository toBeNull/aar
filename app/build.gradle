apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    productFlavors {
        rootProject.ext.SDKProperties.each {
            productFlavors.create(it.product).setApplicationId(it.packageName)
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    repositories {
        flatDir {
            dirs 'libs'
        }
    }
}

dependencies {
    if (rootProject.ext.debug) {
        rootProject.ext.SDKProperties.each {
            println "configurations: ${it.product}Compile"
            dependencies.add("${it.product}Compile", project(
                    path: ':sdk', configuration: "${it.product}Release")
            )
        }
    } else {
//        compile(name: 'sdk-release', ext: 'aar')
    }
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportVersion}"
    testCompile 'junit:junit:4.12'
}
